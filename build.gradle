plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.0' apply false
    id 'io.kotest' version '6.0.1' apply false
    id 'com.diffplug.spotless' version "7.2.1" apply false
    id "org.jlleitschuh.gradle.ktlint" version "13.1.0" apply false
    id 'idea'
}

group = 'org.eventhorizonlab.kyoriadventuredsl'
version = '0.0.1-PRE-ALPHA'

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'io.kotest'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    kotlin {
        jvmToolchain(24)
    }

    apply from: rootProject.file('gradle/repositories.gradle')
    apply from: rootProject.file('gradle/versions.gradle')
    apply from: rootProject.file('gradle/dependencies.gradle')

    tasks.withType(Jar).configureEach {
        archiveBaseName.set("${rootProject.name}-${project.name}")
        archiveVersion.set(rootProject.version as String)
        destinationDirectory.set(rootProject.layout.buildDirectory.dir("libs"))
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        reports {
            junitXml.required.set(false)
        }
        systemProperty("gradle.build.dir", rootProject.layout.buildDirectory.asFile.get().absolutePath)
    }

    apply from: rootProject.file('gradle/spotless.gradle')
}

apply from: 'gradle/tasks.gradle'
